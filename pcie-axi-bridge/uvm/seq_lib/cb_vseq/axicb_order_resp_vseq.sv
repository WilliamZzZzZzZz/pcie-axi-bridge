`ifndef AXICB_ORDER_RESP_VSEQ_SV
`define AXICB_ORDER_RESP_VSEQ_SV

class axicb_order_resp_vseq extends axicb_conc_base_vseq;
    `uvm_object_utils(axicb_order_resp_vseq)      

    function new(string name = "axicb_order_resp_vseq");
        super.new(name);
    endfunction

    virtual task body();
        super.body();
        `uvm_info(get_type_name(), "========== order_resp_test_start ==========", UVM_LOW)

        threads_depth_test();
        outstanding_depth_test();
        ordering_protection_test();
        response_arb_test();

        `uvm_info(get_type_name(), "========== order_resp_test_end ==========", UVM_LOW)
    endtask

    //outstanding depth = min(S_ACCEPT=16, M_ISSUE=4), so test depth 4 is enough
    local task outstanding_depth_test();
        fork
            wait_upstream_done(4, 4);
            begin
                same_id_same_slave_write_outstanding_accept(4);
                same_id_same_slave_read_outstanding_accept(4);
            end
        join
    endtask

    //S_THREADS=2, means upstream's slave port max allow 2 different ID transaction concurrently
    local task threads_depth_test();
        fork
            wait_upstream_done(2, 2);
            begin
                unique_id_thread_write(2);
                unique_id_thread_read(2);
            end
        join
    endtask

    local task ordering_protection_test();
        fork
            wait_upstream_done(2, 2);
            begin
                same_id_diff_slave_write();
                same_id_diff_slave_read();
            end
        join
    endtask

    local task response_arb_test();
        fork
            wait_upstream_done(2, 2);
            begin
                diff_id_diff_slave_write();
                diff_id_diff_slave_read();
            end
        join
    endtask

    local task same_id_same_slave_write_outstanding_accept(int unsigned tested_depth);
        set_slave_b_resp_delay(0, 100);
        fork
            expect_upstream_outstanding_depth(WRITE, 0, tested_depth);
            repeat(tested_depth) begin
                do_nblock_write(0, s0_base_addr, BURST_LEN_4BEATS, INCR, BURST_SIZE_4BYTES, 8'hBE);
            end
        join
        clear_slave_b_resp_delay(0);
    endtask

    local task same_id_same_slave_read_outstanding_accept(int unsigned tested_depth);
        set_slave_r_resp_delay(1, 150);
        fork
            expect_upstream_outstanding_depth(READ, 1, tested_depth);
            repeat(tested_depth) begin
                do_nblock_read(1, s1_mid_addr, BURST_LEN_4BEATS, INCR, BURST_SIZE_4BYTES, 8'hCD);
            end
        join
        clear_slave_r_resp_delay(1);
    endtask

    local task unique_id_thread_write(int unsigned test_threads);
        set_slave_b_resp_delay(0, 100);
        fork
            expect_upstream_unique_id_threads(WRITE, 0, test_threads);
            begin
                do_nblock_write(0, s0_mid_addr, BURST_LEN_4BEATS, INCR, BURST_SIZE_4BYTES, 8'hA1);
                do_nblock_write(0, s0_mid_addr, BURST_LEN_4BEATS, INCR, BURST_SIZE_4BYTES, 8'hA2);
            end
        join
        clear_slave_b_resp_delay(0);
    endtask

    local task unique_id_thread_read(int unsigned test_threads);
        set_slave_r_resp_delay(1, 100);
        fork
            expect_upstream_unique_id_threads(READ, 1, test_threads);
            begin
                do_nblock_read(1, s1_mid_addr, BURST_LEN_4BEATS, INCR, BURST_SIZE_4BYTES, 8'hA3);
                do_nblock_read(1, s1_mid_addr, BURST_LEN_4BEATS, INCR, BURST_SIZE_4BYTES, 8'hA4);
            end
        join
        clear_slave_r_resp_delay(1);
    endtask

    local task same_id_diff_slave_write();
        set_slave_b_resp_delay(0, 100);
        fork
            expect_same_id_diff_slave_aw_block(0, 0, 1, 8'hA3);
            begin
                do_nblock_write(0, s0_mid_addr, BURST_LEN_4BEATS, INCR, BURST_SIZE_4BYTES, 8'hA3);
                do_nblock_write(0, s1_mid_addr, BURST_LEN_4BEATS, INCR, BURST_SIZE_4BYTES, 8'hA3);            
            end
        join
        clear_all_slave_b_resp_delay();
    endtask

    local task same_id_diff_slave_read();
        set_slave_r_resp_delay(0, 100);
        fork
            expect_same_id_diff_slave_ar_block(0, 0, 1, 8'hA7);
            begin
                do_nblock_read(0, s0_mid_addr, BURST_LEN_4BEATS, INCR, BURST_SIZE_4BYTES, 8'hA7);
                do_nblock_read(0, s1_mid_addr, BURST_LEN_4BEATS, INCR, BURST_SIZE_4BYTES, 8'hA7);            
            end
        join
        clear_all_slave_r_resp_delay();
    endtask

    local task diff_id_diff_slave_write();
        set_slave_b_resp_delay(0, 105);
        set_slave_b_resp_delay(1, 100);
        fork
            expect_same_master_b_resp_contention(0);
            begin
                do_nblock_write(0, s0_base_addr, BURST_LEN_4BEATS, INCR, BURST_SIZE_4BYTES, 8'hB3);
                do_nblock_write(0, s1_base_addr, BURST_LEN_4BEATS, INCR, BURST_SIZE_4BYTES, 8'hB4);
            end
        join
        clear_all_slave_b_resp_delay();
    endtask

    local task diff_id_diff_slave_read();
        set_slave_r_resp_delay(0, 105);
        set_slave_r_resp_delay(1, 100);
        fork
            expect_same_master_r_resp_contention(1);
            begin
                do_nblock_read(1, s0_base_addr, BURST_LEN_4BEATS, INCR, BURST_SIZE_4BYTES, 8'hB5);
                do_nblock_read(1, s1_base_addr, BURST_LEN_4BEATS, INCR, BURST_SIZE_4BYTES, 8'hB6);
            end
        join
        clear_all_slave_r_resp_delay();
    endtask
endclass

`endif 
